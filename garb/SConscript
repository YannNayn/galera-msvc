# Copyright (C) 2011 Codership Oy <info@codership.com>

Import('env', 'libboost_program_options')

garb_env = env.Clone()
if sys.platform.find("win")==0:
    target = 'garbd.exe'
    garb_env.Prepend(LIBS=File('galerautils/src/galerautils.lib'))
    garb_env.Prepend(LIBS=File('galerautils/src/galerautils++.lib'))
    garb_env.Prepend(LIBS=File('gcomm/src/gcomm.lib'))
    garb_env.Prepend(LIBS=File('gcs/src/gcs4garb.lib'))
else:
    target = 'garbd'
    garb_env.Prepend(LIBS=File('#/galerautils/src/galerautils.a'))
    garb_env.Prepend(LIBS=File('#/galerautils/src/galerautils++.a'))
    garb_env.Prepend(LIBS=File('#/gcomm/src/gcomm.a'))
    garb_env.Prepend(LIBS=File('#/gcs/src/gcs4garb.a'))
    
if libboost_program_options:
    garb_env.Append(LIBS=libboost_program_options)

# special environment for garb_config.cpp
conf_env = garb_env.Clone()
Import('GALERA_VER', 'GALERA_REV')
conf_env.Append(CPPFLAGS = ' -DGALERA_VER=\\"' + GALERA_VER + '\\"')
conf_env.Append(CPPFLAGS = ' -DGALERA_REV=\\"' + GALERA_REV + '\\"')

garb = garb_env.Program(target = target,
                        source = Split('''
                                       garb_logger.cpp
                                       garb_gcs.cpp
                                       garb_recv_loop.cpp
                                       garb_main.cpp
                                   ''')
                                   +
                                   conf_env.SharedObject(['garb_config.cpp'])
                       )
